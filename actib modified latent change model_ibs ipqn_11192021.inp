 Data:
    File is "C:\Users\kdasil5\Desktop\
    Updated_Indirect_effect_01312023\
    Bayesian Latent Change model\
    Data_for_Kevin_3_Nov_2021\Experimental data\
    ACTIB long data std_3 Nov 2021.dat" ;
  Variable:
    Names are 
        tcbt ibs0 ibs1 ibs2 ibs3 ipqn0 ipqn1 ipqn2 ipqn3 ipqn0s ipqn1s ipqn2s
        ipqn3s ibs0s ibs1s ibs2s ibs3s;     
    USEVARIABLES ARE ibs0s ibs1s ibs2s ibs3s ipqn0s ipqn1s ipqn2s ipqn3s tcbt ;
    Missing are all (99) ;

    ANALYSIS: BOOTSTRAP = 1000 ;

    MODEL:         
                             
                      fm0 by ipqn0s ipqn1s ipqn2s ipqn3s (estm)  ;
                      fm1 by ipqn1s ipqn2s ipqn3s (estm) ;
                      fm2 by ipqn2s ipqn3s (estm) ;
                      fm3 by ipqn3s@1 ;
                      
                      ipqn0s ipqn1s ipqn2s ipqn3s (varipq);
  
                      fy0 by ibs0s ibs1s ibs2s ibs3s (esty) ;
                      fy1 by ibs1s ibs2s ibs3s (esty) ;
                      fy2 by ibs2s ibs3s (esty) ;
                      fy3 by ibs3s@1 ;                       
                      
                      ibs0s ibs1s ibs2s ibs3s (varibs);

                      fm1 on tcbt*0 (a11) ;

                      fm2 on tcbt*0 (a12) ;

                      fm3 on tcbt@0 ;

                      fy1 on tcbt*0 (c11) ;

                      fy2 on tcbt*0 (c12) ;

                      fy3 on tcbt@0  ;

                      fy1 on fm1*0 (b) ;
                      fy2 on fm2*0 (b) ;
                      fy3 on fm3*0 (b) ;


                      fm0 with fy0*0 ;
                      fm1 with fy1@0 ;
                      fm2 with fy2@0 ;
                      fm3 with fy3@0 ;
!TRIED ALLOWING FM3 AND FY3 TO CORRELATE TO SEE IF HELPS WITH NEG RES VAR OF 
! FY3/CORREL GREATER THAN 1 BET FY3 AND FM3, THIS DID HELP
                      
                      fy1 with fy2@0 ;
                      fy2 with fy3@0 ;
                      fy1 with fy3@0 ;
                      fm1 with fm2@0 ;
                      fm2 with fm3@0 ;
                      fm1 with fm3@0 ;

!TRIED ALLOWING THESE CORRELATIONS TO BE FREE TO SEE IF HELPS WITH NEG RES VAR
! OF FY3/CORREL GREATER THAN 1 BET FY3 AND FM3, DIDN'T HELP
                      ipqn0s with ibs0s*0 (4);
                      ibs1s with ipqn1s*0  (4);
                      ibs2s with ipqn2s*0 (4);
                      ibs3s with ipqn3s*0  (4);  

MODEL INDIRECT:       ibs1s ind tcbt ;
                      ibs2s ind tcbt ;

MODEL CONSTRAINT:
                      NEW(TP1, INT1, TP21, TP22, INT2, TP31, TP32, INT3);

                      !FIRST TIME POINT
                      TP1 = a11*b*1;
                      INT1 = TP1;

                      !SECOND TIME POINT
                      TP21 = a11*b*esty;
                      TP22 = a12*b*1;
                      INT2 = TP21 + TP22;

                      !THIRD TIME POINT
                      TP31 = a11*b*esty;
                      TP32 = a12*b*esty;
                      INT3 = TP31 + TP32;


OUTPUT: CINTERVAL(BOOTSTRAP) tech4 sampstat STDYX;